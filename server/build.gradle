plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'java'
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

group = 'by.flitt'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

tasks.register('processFrontendResources', Copy) {
	// Directory containing the artifacts in the frontend project
	def frontendBuildDir = file("${project(':frontend').projectDir}/dist")
	// Directory where the frontend artifacts must be copied to be packaged alltogether with the backend by the 'war'
	// plugin.
	def frontendResourcesDir = file("${project.buildDir}/resources/main/public")

	group 'Frontend'
	description 'Process frontend resources'
	dependsOn project(':frontend').tasks.named('assembleFrontend')

	from frontendBuildDir
	into frontendResourcesDir
}

tasks.named('processResources') {
	dependsOn tasks.named('processFrontendResources')
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:2.5.0'
	implementation 'org.apache.poi:poi:3.15'
	implementation 'org.apache.poi:poi-ooxml:3.15'
	implementation 'com.google.guava:guava:30.0-jre'
	implementation 'net.logstash.logback:logstash-logback-encoder:6.6'

	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.0'
}

test {
	useJUnitPlatform()
}
